// Kotlin support https://kotlinlang.org/docs/reference/using-gradle.html
buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version'
    }
}

plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building an application
    id 'application'

    // ANTLR parser generator
    id 'antlr'

    // JVM target for Kotlin compiler
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.+'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8' // use JDK8 extended version since we're that's the target
    compile 'com.xenomachina:kotlin-argparser:2.0.7'
}

// Define the main class for the application
mainClassName = 'rml.CompilerKt'

// use System.in in child processes
run.standardInput = System.in

jar {
    // make it executable
    manifest {
        attributes 'Main-Class': 'rml.CompilerKt'
    }

    // include dependencies (fat-JAR)
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// first generate ANTLR parser, then compile source code
compileKotlin.dependsOn generateGrammarSource

generateGrammarSource {
    // generate visitor rather than listener
    arguments += ['-visitor', '-no-listener']
}